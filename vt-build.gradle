/**
 * vt-build.gradle
 */

apply plugin: 'groovy'

ext {
    groovyVersion   = '2.4.7'
}

sourceSets {
    main {
        groovy {
            srcDirs = ['Keywords', 'Include/scripts/groovy']
	    srcDir 'Libs'
        }
    }
}

configurations {
    generateDocs
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    generateDocs "org.codehaus.groovy:groovy-all:${groovyVersion}"
}

/**
 * generate Groovydoc for the com.kazurayam.visualtesting package
 */
task groovydoc(type: Groovydoc, overwrite: true) {
    source = sourceSets.main.groovy
    classpath = configurations.compile
    groovyClasspath = project.configurations.generateDocs
    include 'com/kazurayam/visualtesting/*'
    exclude '**/*Test.groovy'
}
task publishGroovydoc(type: Copy) {
    from 'build/docs/groovydoc'
    into 'docs/api'
}
groovydoc.finalizedBy publishGroovydoc


// ===== tasks to generate zip files for distributing Visual Testing in Katalon Studio =====

task cleanDist() {
    doLast {
        delete fileTree('build/dist') {
            include '**'
        }
    }
}

task createGradlewPackaged(type: Zip) {
    archiveFileName = "gradlew-packaged.zip"
    destinationDirectory = file("$buildDir/dist")
    from(".") {
        // include the gradle wrapper
        include "gradlew"
        include "gradlew.bat"
        include "gradlewks.bat"    // customized launcher using Java bundled in %KATALONSTUDIO_HOME%\jre
        include "gradle/**/*"

        // include initializing script for Visual Testing in Katalon Studio
        include "vt-init.gradle"
    }
}


task createVTComponentsZip(type:Zip) {
    archiveFileName = "vt-components.zip"
    destinationDirectory = file("$buildDir/dist")
    // include Visual Testing components
    from (".") {
        include "vt-drivers.gradle"
        include "Test Cases/VT/**"
        include "Scripts/VT/**"
        include "Test Listeners/*"
        include "Test Suites/VT/**"
        include "Keywords/com/kazurayam/visualtesting/**"
        include "run-console-mode.*"
    }

    // include Git related configurations
    from (".") {
        include ".gitignore"
    }
}

task createVTExampleZip(type: Zip) {
    archiveFileName = "vt-example.zip"
    destinationDirectory = file("$buildDir/dist")

    // include sample codes of capturing web pages and examining image pairs
    from(".") {
        include "Object Repository/CURA/**"
        include "Test Cases/CURA/**"
        include "Scripts/CURA/**"
        include "Test Suites/CURA/**"
        include "Profiles/**/CURA*"
    }
}

task distributables() {
    dependsOn 'createGradlewPackaged'
    dependsOn 'createVTComponentsZip'
    dependsOn 'createVTExampleZip'
    tasks.findByName('createGradlewPackaged').mustRunAfter 'cleanDist'
    tasks.findByName('createVTComponentsZip').mustRunAfter 'cleanDist'
    tasks.findByName('createVTExampleZip').mustRunAfter 'cleanDist'
}

defaultTasks = [ 'distributables' ]
